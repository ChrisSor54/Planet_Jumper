# Built-in process entry points.
# The kernel runs these processes automatically.
# Each process must end with an 'exit' instruction.

#-------------------------------------------------------------------------------
bmk "About"

# This is primarily a 2D n-body gravity simulation, but it also features
# a playable character with which to jump around the various gravitational bodies.

# Some credit goes to Flatik, who uploaded a similar gravity simulation. Although none
# of the code was copied (despite how surprisingly similar certain elements turned out)
# I did incorporate some of their optimizations in a small few places, so I'd like to
# give them a shoutout nonetheless :)

# Controls:
# BTN_UP: Charge your jump with more 'oomph'
# BTN_LEFT / BTN_RIGHT: Move along your current body
# BTN_A: Jump away from the body you're standing on.
# BTN_B: Kill player velocity


#-------------------------------------------------------------------------------
bmk "DATA"

PLAYER_SPRITESHEET: emb file "assets/astronaut.png" # To be implemented

#-------------------------------------------------------------------------------
bmk "CONSTANTS"

# Simulation
def G 1.0 # This makes gravity a lot stronger but more fun :)
#def G 6.6743*(10**-11)
def MAX_BODIES 20
def BODY_ELASTICITY 0.8

# Visuals
def TIME_SCALE 1.0
def BODY_LUMA 200

def ZOOM_STEP 2.0

def PLAYER_SPRITE_WIDTH 8
def PLAYER_SPRITE_WIDTH_F 8.0

def CENTER_CAMERA true # Whether the camera follows the player or not
def CAMERA_SPEED 0.5
def CAMERA_OFFSET 50.0
def DRAW_VELOCITIES true # Whether to draw velocity vectors
def VELOCITY_VISUAL_SCALE 1.0 # How much velocity vectors should be scaled

def ZOOM_OUT_VAL 4.0
def ZOOM_IN_VAL 1.0

def SCREEN_WIDTH_F 320.0
def SCREEN_HEIGHT_F 240.0
def CENTER_X_F SCREEN_WIDTH_F/2.0
def CENTER_Y_F SCREEN_HEIGHT_F/2.0

Input:
    def .JUMP BTN_A
    def .KILL_VELOCITY BTN_B
    def .TOGGLE_ZOOM BTN_X

#-------------------------------------------------------------------------------
bmk "STRUCTS"

#-------------------------------------------------------------------------------
sbmk "Player"

PLAYER:
    # Constants
    def .SPEED 100.0
    def .THRUSTER_STRENGTH 100.0
    def .MIN_JUMP_CHARGE 100.0
    def .MAX_JUMP_CHARGE 200.0
    def .JUMP_CHARGE_SPEED -(.MAX_JUMP_CHARGE-.MIN_JUMP_CHARGE)/0.8
    # Properties
    ## Physics
    .x: emb f32t 0.0 # X Position
    .y: emb f32t 0.0 # Y Position
    .velx: emb f32t 0.0 # X Velocity (km/s)
    .vely: emb f32t 0.0 # Y Velocity (km/s)
    .movex: emb f32t 0.0 # X Move Velocity
    .movey: emb f32t 0.0 # Y Move Velocity
    .rot: emb f32t 0.0 # Rotation (Radians)
    .mass: emb f32t 7.0 # Mass (kg)
    .collision_radius: emb f32t PLAYER_SPRITE_WIDTH_F/2.0 # Radius of collision circle

    .jump_charge: emb f32t PLAYER.MIN_JUMP_CHARGE
    .grounded: emb u8t false
    .parent_body_index: emb u8t 0
    .is_flying: emb u8t false
    .flip_sprite: emb u8t false

#-------------------------------------------------------------------------------
sbmk "Body"

BODY:
    # Properties
    .x: emb f32t 0.0 # X Position
    .y: emb f32t 0.0 # Y Position
    .velx: emb f32t 0.0 # X Velocity (km/s)
    .vely: emb f32t 0.0 # Y Velocity (km/s)
    .rot: emb f32t 0.0 # Rotational velocity
    .radius: emb f32t 1.0 # Radius (km)
    .mass: emb f32t 1.0 # Mass (kg)
    # Offsets
    def .X (.x - BODY)
    def .Y (.y - BODY)
    def .VX (.velx - BODY)
    def .VY (.vely - BODY)
    def .ROT (.rot - BODY)
    def .R (.radius - BODY)
    def .M (.mass - BODY)
    def .SIZE ($ - BODY)

#-------------------------------------------------------------------------------
sbmk "Smoke Particle"

SMOKE:
    # Constants
    def .MAX_PARTICLE_COUNT 60
    def .MIN_VEL 1.0
    def .MAX_VEL 10.0
    def .MIN_ANGLE_OFFSET -5.0
    def .MAX_ANGLE_OFFSET 50
    def .DEFAULT_LIFESPAN 0.5

    # Properties
    .x: emb f32t 0.0 # X Position
    .y: emb f32t 0.0 # Y Position
    .velx: emb f32t 0.0 # X Velocity (km/s)
    .vely: emb f32t 0.0 # Y Velocity (km/s)
    .lifespan: emb f32t 10.0
    # Offsets
    def .X (.x - SMOKE)
    def .Y (.y - SMOKE)
    def .VX (.velx - SMOKE)
    def .VY (.vely - SMOKE)
    def .LIFESPAN (.lifespan - SMOKE)
    def .SIZE ($ - SMOKE)


#-------------------------------------------------------------------------------
sbmk "Misc."


Commands:
    .ZOOM_IN: emb string "ZOOM_IN"
    .ZOOM_OUT: emb string "ZOOM_OUT"

Strings:
    .jump_charge: emb string "Jump Charge: "
    .zoom: emb string "Set Distance Scale to: "

#-------------------------------------------------------------------------------
bmk "VARIABLES"

buffer: res u8t MAX_TERMINAL_INPUT_SIZE

bodies: res u8t BODY.SIZE*MAX_BODIES
bodies_count: emb u8t 0

smoke: res u8t SMOKE.SIZE*SMOKE.MAX_PARTICLE_COUNT
smoke_count: emb u8t 0
smoke_can_spawn: emb u8t false

has_player_collided: emb u8t false
skip_player_collision_check: emb u8t false

zoom_toggled: emb u8t true
distance_scale: emb f32t 1.0 # Camera Zoom

dt: emb f32t 0.0 # DeltaTime

#-------------------------------------------------------------------------------
bmk "-------------------------"

#-------------------------------------------------------------------------------
bmk "PROCESSES"

#-------------------------------------------------------------------------------
sbmk "Start"
_start: # Runs once when the VM starts.
    # Initialize your game state here.

    mov a0, 0.0 # X
    mov a1, 140.0 # Y
    mov a2, 0.0 # VX
    mov a3, 0.0 # VY
    mov a4, 0.0 # ROTATION
    mov a5, 70.0 # RADIUS
    mov a6, 1000000.0 # MASS
    cal add_body

    mov a0, 640.0 # X
    mov a1, 140.0 # Y
    mov a2, 0.0 # VX
    mov a3, 40.0 # VY
    mov a4, 0.0 # ROTATION
    mov a5, 30.0 # RADIUS
    mov a6, 100000.0 # MASS
    cal add_body

    #mov a0, 896.0 # X
    #mov a1, 140.0 # Y
    #mov a2, -1.0 # VX
    #mov a3, 68.0 # VY
    #mov a4, 0.0 # ROTATION
    #mov a5, 70.0 # RADIUS
    #mov a6, 0.000000001 # MASS
    #cal add_body

    #mov a0, 0.0 # X
    #mov a1, -490.0 # Y
    #mov a2, 40.0 # VX
    #mov a3, 0.0 # VY
    #mov a4, 0.0 # ROTATION
    #mov a5, 350.0 # RADIUS
    #mov a6, 30000.0 # MASS
    #cal add_body

    exit

#-------------------------------------------------------------------------------
sbmk "Update"
_update: # Runs at 60 Hz.
    # Write your game logic here.
    syscall SYS_GET_UPDATE_DELTA
    str f32t, dt, a0

    str u8t, skip_player_collision_check, false
    str u8t, has_player_collided, false
    str u8t, smoke_can_spawn, false

    psh s0
    mov s0, zr
    cal update_gravity
    cal update_collisions
    cal check_input
    cal update_positions
    cal update_smoke
    @iterate_update: # Iterate for larger timescales
        fadd s0, 1.0
        cmp flt, s0, TIME_SCALE
        jfs @end+
        cal update_gravity
        cal update_collisions
        cal update_positions
        cal update_smoke
        yield
        jmp @iterate_update-
    @end:
    .if_center_camera:
        mov cr, CENTER_CAMERA
        jfs @endif+
        cal center_camera
    @endif:
    pop s0
    exit

#-------------------------------------------------------------------------------
sbmk "Draw"
_draw: # Runs at 60 Hz and updates the front buffer.
    # Draw graphics to the screen here.
    cal draw_bodies
    cal draw_player
    cal draw_smoke
    exit

#-------------------------------------------------------------------------------
sbmk "Input"
_input: # Runs when input state changes.
    # React to player input here.

    syscall SYS_GET_INPUT
    # Toggle Zoom
    and t0, a0, Input.TOGGLE_ZOOM
    cmp neq, t0, 0
    mov t0, cr

    @toggle_zoom:
        cmp eq, t0, 1
        jfs @end+
        mov a0, ZOOM_IN_VAL
        lod u8t, cr, zoom_toggled
        mvc a0, ZOOM_OUT_VAL
        mov t0, true
        mvc t0, false
        str u8t, zoom_toggled, t0
        cal set_distance_scale
    @end:

    exit

#-------------------------------------------------------------------------------
sbmk "Terminal Input"
_terminal_input:
    # Reads the terminal input string into memory.
    mov a0, buffer
    mov a1, MAX_TERMINAL_INPUT_SIZE
    syscall SYS_READ_TERMINAL_INPUT
    cal check_commands
    exit

#-------------------------------------------------------------------------------
bmk "-------------------------"

#-------------------------------------------------------------------------------
bmk "FUNCTIONS"


#-------------------------------------------------------------------------------
bmk "Spawning"

#-------------------------------------------------------------------------------
sbmk "Add Body"
add_body:
    # > (f32t) a0..a1: x, y position (from center of screen)
    # > (f32t) a2..a3: x, y velocity
    # > (f32t) a4: rotation speed
    # > (f32t) a5: radius
    # > (f32t) a6: mass

    lod u8t, t0, bodies_count
    cmp lt, t0, MAX_BODIES
    jfs @end+

    cea bodies, t0, BODY.SIZE
    #lod f32t, t1, distance_scale
    #fmul a0, t1
    #fmul a1, t1
    ste f32t, BODY.X, a0
    ste f32t, BODY.Y, a1
    ste f32t, BODY.VX, a2
    ste f32t, BODY.VY, a3
    ste f32t, BODY.ROT, a4
    ste f32t, BODY.R, a5
    ste f32t, BODY.M, a6
    inc t0
    str u8t, bodies_count, t0
    @end:
    ret

#-------------------------------------------------------------------------------
sbmk "Spawn Smoke"
spawn_smoke:
    # > a0: smoke index

    lod f32t, t0, PLAYER.x
    lod f32t, t1, PLAYER.y
    lod f32t, t2, PLAYER.velx
    lod f32t, t3, PLAYER.vely
    lod f32t, t4, PLAYER.rot
    lod f32t, t5, PLAYER.collision_radius

    fadd t4, PI
    frnd t7, SMOKE.MIN_ANGLE_OFFSET, SMOKE.MAX_ANGLE_OFFSET
    fadd t6, t4, t7
    fcos t7, t6
    fsin t8, t6
    frnd t9, SMOKE.MIN_VEL, SMOKE.MAX_VEL
    fmul t7, t9
    fmul t8, t9

    #fadd t7, t2
    #fadd t8, t3
    # Position offset vector
    fcos t2, t4
    fsin t3, t4
    fmul t2, t5
    fmul t3, t5
    lod f32t, t10, distance_scale
    fdiv t2, t10
    fdiv t3, t10
    fadd t2, t0
    fadd t3, t1

    cea smoke, a0, SMOKE.SIZE
    ste f32t, SMOKE.X, t2
    ste f32t, SMOKE.Y, t3
    ste f32t, SMOKE.VX, t7
    ste f32t, SMOKE.VY, t8
    ste f32t, SMOKE.LIFESPAN, SMOKE.DEFAULT_LIFESPAN

    ret


#-------------------------------------------------------------------------------
bmk "Input"

#-------------------------------------------------------------------------------
sbmk "Check Input"
check_input:
    # Get input states
    # s0: Left - Right
    # s1: Up - Down
    # s2: Jump
    # s3: Kill Velocity

    vpsh s0..s3

    syscall SYS_GET_INPUT
    and t0, a0, BTN_LEFT
    cmp neq, t0, 0
    fctf t0, cr
    and t1, a0, BTN_RIGHT
    cmp neq, t1, 0
    fctf t1, cr
    and t2, a0, BTN_UP
    cmp neq, t2, 0
    fctf t2, cr
    and t3, a0, BTN_DOWN
    cmp neq, t3, 0
    fctf t3, cr
    fneg t0
    fneg t2
    fadd s0, t0, t1
    fadd s1, t2, t3

    and t0, a0, Input.JUMP
    cmp neq, t0, 0
    mov s2, cr

    and t0, a0, Input.KILL_VELOCITY
    cmp neq, t0, 0
    mov s3, cr

    str u8t, PLAYER.is_flying, false

    .is_grounded:
        lod u8t, cr, PLAYER.grounded
        jfs @end+
        str f32t, PLAYER.movex, 0.0 # Reset movement
        str f32t, PLAYER.movey, 0.0
        cmp neq, s1, 0
        jfs @end+
        mov a0, s1
        cal charge_jump
    @end:

    @move:
        cmp neq, s0, 0
        mov t0, cr
        cmp neq, s1, 0
        orr cr, t0
        jfs @end+
        mov a0, s0
        mov a1, s1
        cal player_move
    @end:

    @jump:
        cmp eq, s2, 1
        jfs @end+
        cal player_jump
    @end:

    @kill_velocity:
        cmp eq, s3, 1
        jfs @end+
        cal player_kill_velocity
    @end:


    vpop s0..s3
    ret

#-------------------------------------------------------------------------------
sbmk "Player Move"
player_move:
    # a0: horizontal direction
    # a1: vertical direction

    .if_grounded:
        lod u8t, cr, PLAYER.grounded
        jfs @else+
        cmp neq, a0, 0.0
        jfs @endif+
        lod u8t, t0, PLAYER.parent_body_index
        cea bodies, t0, BODY.SIZE
        lod f32t, t0, PLAYER.x
        lod f32t, t1, PLAYER.y
        lod f32t, t2, PLAYER.velx
        lod f32t, t3, PLAYER.vely
        lde f32t, t4, BODY.X
        lde f32t, t5, BODY.Y

        # Calculate normal vector
        fsub t6, t0, t4 # dx
        fsub t7, t1, t5 # dy
        fpow t8, t6, 2.0
        fpow t9, t7, 2.0
        fadd t8, t9 # r^2
        fsqrt t8 # r
        fdiv t6, t8 # dx/r
        fdiv t7, t8 # dy/r
        fneg t0, t7 # x^
        mov t1, t6 # y^

        fmul t10, a0, PLAYER.SPEED
        fmul t0, t10
        fmul t1, t10
        fadd t2, t0
        fadd t3, t1
        str f32t, PLAYER.movex, t2
        str f32t, PLAYER.movey, t3

        .flip_sprite:
            cmp neq, a0, 0.0
            jfs @end+
            mov t0, 0
            cmp flt, a0, 0.0
            mvc t0, 1
            str u8t, PLAYER.flip_sprite, t0
        @end:
        jmp @endif+
    @else:
        lod f32t, t0, PLAYER.velx
        lod f32t, t1, PLAYER.vely
        lod f32t, t2, dt
        fmul a0, PLAYER.THRUSTER_STRENGTH
        fmul a1, PLAYER.THRUSTER_STRENGTH
        ffma t0, a0, t2, t0
        ffma t1, a1, t2, t1
        str f32t, PLAYER.velx, t0
        str f32t, PLAYER.vely, t1
        str u8t, smoke_can_spawn, true
        str u8t, PLAYER.is_flying, true
        # Update rotation to match movement
        fatan2 t2, a1, a0
        fmod t2, 2.0*PI
        mov t3, 0.0
        cmp flt, t2, 0.0
        mvc t3, 2.0*PI
        fadd t2, t3
        str f32t, PLAYER.rot, t2


    @endif:
    ret

#-------------------------------------------------------------------------------
sbmk "Charge Jump"
charge_jump:
    # > a0: UP/DOWN input

    lod u8t, cr, PLAYER.grounded
    jfs @end+

    fmul t0, a0, PLAYER.JUMP_CHARGE_SPEED
    lod f32t, t1, dt
    fmul t0, t1

    lod f32t, t1, PLAYER.jump_charge
    fadd t0, t1

    fclp t0, PLAYER.MIN_JUMP_CHARGE, PLAYER.MAX_JUMP_CHARGE
    str f32t, PLAYER.jump_charge, t0

    .if_charge_changed:
        cmp neq, t0, t1
        jfs @end+
        mov a0, Strings.jump_charge
        syscall SYS_PRINT_STRING
        fcti a0, t0
        syscall SYS_PRINT_LINE_INT
    @end:
    ret

#-------------------------------------------------------------------------------
sbmk "Player Jump"
player_jump:
    lod u8t, t0, PLAYER.grounded
    cmp eq, t0, true
    jfs @end+
    lod u8t, t0, PLAYER.parent_body_index
    cea bodies, t0, BODY.SIZE
    lod f32t, t0, PLAYER.x
    lod f32t, t1, PLAYER.y
    lod f32t, t2, PLAYER.velx
    lod f32t, t3, PLAYER.vely
    lde f32t, t4, BODY.X
    lde f32t, t5, BODY.Y

    # Calculate normal vector
    fsub t6, t0, t4 # dx
    fsub t7, t1, t5 # dy
    fpow t8, t6, 2.0
    fpow t9, t7, 2.0
    fadd t8, t9 # r^2
    fsqrt t8 # r
    fdiv t6, t8 # dx/r
    fdiv t7, t8 # dy/r
    lod f32t, t9, PLAYER.jump_charge

    # Jump impulse vector
    fmul t6, t9
    fmul t7, t9

    fadd t2, t6
    fadd t3, t7
    str f32t, PLAYER.velx, t2
    str f32t, PLAYER.vely, t3
    #str u8t, false, PLAYER.grounded

    # Apply impulse to parent body
    fneg a0, t6
    fneg a1, t7
    lod u8t, a2, PLAYER.parent_body_index
    cal apply_impulse


    @end:
    ret

#-------------------------------------------------------------------------------
sbmk "Player Kill Velocity"
player_kill_velocity:
    # Kills player velocity. Useful for recovering after flinging yourself

    lod f32t, t0, PLAYER.velx
    lod f32t, t1, PLAYER.vely

    .if_player_grounded: # If player is grounded, match parent body velocity
        lod u8t, cr, PLAYER.grounded
        jfs @else+
        lod u8t, t2, PLAYER.parent_body_index
        cea bodies, t2, BODY.SIZE
        lde f32t, t3, BODY.VX
        lde f32t, t4, BODY.VY
        str f32t, PLAYER.velx, t3
        str f32t, PLAYER.vely, t4
        jmp @end+
    @else:
        lod f32t, t0, PLAYER.velx
        lod f32t, t1, PLAYER.vely
        lod f32t, t2, dt
        fmul t2, PLAYER.THRUSTER_STRENGTH
        fdiv t2, 5.0
        flrp t3, t0, 0.0, t2
        flrp t4, t1, 0.0, t2
        str f32t, PLAYER.velx, t3
        str f32t, PLAYER.vely, t4
        str u8t, smoke_can_spawn, true
        str u8t, PLAYER.is_flying, true
        # Update rotation
        fatan2 t2, t1, t0
        fmod t2, 2.0*PI
        mov t3, 0.0
        cmp flt, t2, 0.0
        mvc t3, 2.0*PI
        fadd t2, t3
        fadd t2, PI
        str f32t, PLAYER.rot, t2

    @end:
    ret

#-------------------------------------------------------------------------------
bmk "Updates"

#-------------------------------------------------------------------------------
sbmk "Update Gravity"
update_gravity:
    # s0: body1 index
    # s1: body2 index
    vpsh s0..s3

    mov s3, zr # Strongest gravitational influence on player
    mov s0, zr
    @loop_bodies:
        lod u8t, t0, bodies_count
        cmp lt, s0, t0
        jfs @endloop_bodies+
        mov s1, zr

        # Other Bodies
        @loop2:
            lod u8t, t0, bodies_count
            cmp lt, s1, t0
            jfs @endloop2+
            cmp eq, s1, s0
            jtr @skip+

            cea bodies, s0, BODY.SIZE
            lde f32t, a0, BODY.X
            lde f32t, a1, BODY.Y
            lde f32t, a2, BODY.VX
            lde f32t, a3, BODY.VY
            lde f32t, a4, BODY.M
            cea bodies, s1, BODY.SIZE
            lde f32t, a5, BODY.X
            lde f32t, a6, BODY.Y
            lde f32t, a7, BODY.M

            mov t2, a2
            mov t3, a3
            vpsh t2..t3
            cal get_gravity_vector
            vpop t2..t3
            fadd t2, a0 # Apply acceleration
            fadd t3, a1

            cea bodies, s0, BODY.SIZE
            ste f32t, BODY.VX, t2
            ste f32t, BODY.VY, t3

            @skip:
            inc s1
            jmp @loop2-
        @endloop2:


        # Player
        lod f32t, a0, PLAYER.x
        lod f32t, a1, PLAYER.y
        lod f32t, a2, PLAYER.velx
        lod f32t, a3, PLAYER.vely
        lod f32t, a4, PLAYER.mass

        cea bodies, s0, BODY.SIZE
        lde f32t, a5, BODY.X
        lde f32t, a6, BODY.Y
        lde f32t, a7, BODY.M

        mov t2, a2
        mov t3, a3
        vpsh t2..t3
        cal get_gravity_vector
        vpop t2..t3
        fadd t2, a0 # Apply acceleration
        fadd t3, a1
        str f32t, PLAYER.velx, t2
        str f32t, PLAYER.vely, t3

        # Check strongest gravitational influence
        .if_player_not_grounded:
            mov cr, PLAYER.grounded
            jtr@endif+
            .update_parent_body:
                cmp fgt, a2, s3
                jfs @end+
                mov s2, s0
                mov s3, a2
            @end:
            str u8t, PLAYER.parent_body_index, s2
        @endif:

        inc s0
        jmp @loop_bodies-
    @endloop_bodies:
    vpop s0..s3
    ret

#-------------------------------------------------------------------------------
sbmk "Update Positions"
update_positions:
    # s0: body index

    psh s0
    mov s0, zr
    @loop_bodies:
        lod u8t, t0, bodies_count
        cmp lt, s0, t0
        jfs @endloop_bodies+

        cea bodies, s0, BODY.SIZE
        lde f32t, t0, BODY.X
        lde f32t, t1, BODY.Y
        lde f32t, t2, BODY.VX
        lde f32t, t3, BODY.VY

        lod f32t, t5, distance_scale
        fdiv t2, t5
        fdiv t3, t5
        lod f32t, t4, dt
        fmul t2, t4
        fmul t3, t4
        @if_timescale_lt_0:
            cmp flt, TIME_SCALE, 0.0
            jfs @endif+
            fmul t2, TIME_SCALE
            fmul t3, TIME_SCALE
        @endif:
        fadd t0, t2
        fadd t1, t3
        ste f32t, BODY.X, t0
        ste f32t, BODY.Y, t1

        inc s0
        jmp @loop_bodies-
    @endloop_bodies:

    lod f32t, t0, PLAYER.x
    lod f32t, t1, PLAYER.y
    lod f32t, t2, PLAYER.velx
    lod f32t, t3, PLAYER.vely

    lod f32t, t4, PLAYER.movex
    lod f32t, t5, PLAYER.movey
    fadd t2, t4
    fadd t3, t5
    # Apply distance scaling
    lod f32t, t10, distance_scale
    fdiv t2, t10
    fdiv t3, t10

    # Apply deltatime
    lod f32t, t4, dt
    fmul t2, t4
    fmul t3, t4
    @if_timescale_lt_0:
        cmp flt, TIME_SCALE, 0.0
        jfs @endif+
        fmul t2, TIME_SCALE
        fmul t3, TIME_SCALE
    @endif:
    fadd t0, t2
    fadd t1, t3
    str f32t, PLAYER.x, t0
    str f32t, PLAYER.y, t1

    .if_grounded:
        lod u8t, cr, PLAYER.grounded
        jfs @else+
        lod f32t, t0, PLAYER.x
        lod f32t, t1, PLAYER.y
        lod u8t, t2, PLAYER.parent_body_index
        cea bodies, t2, BODY.SIZE
        lde f32t, t3, BODY.X
        lde f32t, t4, BODY.Y
        lde f32t, t5, BODY.R
        lod f32t, t7, PLAYER.collision_radius
        fadd t5, t7
        lod f32t, t6, distance_scale
        fdiv t5, t6

        fsub t6, t0, t3 # dx
        fsub t7, t1, t4 # dy
        fpow t8, t6, 2.0
        fpow t9, t7, 2.0
        fadd t8, t9 # r^2
        fsqrt t8 # r
        fdiv t6, t8 # dx/r
        fdiv t7, t8 # dy/r
        ffma t8, t6, t5, t3
        ffma t9, t7, t5, t4
        str f32t, PLAYER.x, t8
        str f32t, PLAYER.y, t9
        # Update player rotation
        fatan2 t10, t7, t6
        fmod t10, 2.0*PI
        mov t11, 0.0
        cmp flt, t10, 0.0
        mvc t11, 2.0*PI
        fadd t10, t11
        str f32t, PLAYER.rot, t10
        jmp @end+
    @else:
        .is_not_flying:
            lod u8t, cr, PLAYER.is_flying
            jtr @endif+
            lod f32t, t0, PLAYER.velx
            lod f32t, t1, PLAYER.vely
            fatan2 t2, t1, t0
            fmod t2, 2.0*PI
            mov t3, 0.0
            cmp flt, t2, 0.0
            mvc t3, 2.0*PI
            fadd t2, t3
            str f32t, PLAYER.rot, t2
        @endif:
    @end:

    pop s0
    ret

#-------------------------------------------------------------------------------
sbmk "Update Collisions"
update_collisions:
    # s0: body1 index
    # s1..s2: body1 x,y
    # s3..s4: body1 vx, vy
    # s5: body1 radius
    # s6: body1 mass
    # s7: body2 index
    # s8..s9: body2 x,y
    # s10..s11: body2 vx, vy
    # s12: body2 radius
    # s13: body2 mass

    vpsh s0..s13

    mov s0, zr
    @loop_bodies:
        lod u8t, t0, bodies_count
        cmp lt, s0, t0
        jfs @endloop_bodies+
        mov s1, zr
        @loop2:
            lod u8t, t0, bodies_count
            cmp lt, s1, t0
            jfs @endloop2+
            cmp eq, s1, s0
            jtr @skip+
            mov a0, s0
            mov a1, s1
            cal check_collision
            jfs @skip+
            cea bodies, s0, BODY.SIZE
            lde f32t, t0, BODY.X
            lde f32t, t1, BODY.Y
            lde f32t, t2, BODY.VX
            lde f32t, t3, BODY.VY
            lde f32t, t4, BODY.M
            cea bodies, s1, BODY.SIZE
            lde f32t, t5, BODY.X
            lde f32t, t6, BODY.Y
            lde f32t, t7, BODY.VX
            lde f32t, t8, BODY.VY
            lde f32t, t9, BODY.M

            vpsh t0..t9
            vmov a0..a9, t0..
            cal get_collision_vector
            vpop t0..t9
            mov t0, a0
            mov t1, a1

            # Apply impulses
            vpsh t0..t1
            mov a2, s1
            cal apply_impulse
            vpop t0..t1
            fmul a0, -1.0, t0
            fmul a1, -1.0, t1
            mov a2, s0
            cal apply_impulse

            @skip:
            inc s1
            jmp @loop2-
        @endloop2:

        # Player collision
        .if_player_colldiing:
            lod u8t, cr, skip_player_collision_check
            jtr @endif+
            mov a0, s0
            cal check_player_collision
            jfs @else+
            cea bodies, s0, BODY.SIZE
            lde f32t, t0, BODY.X
            lde f32t, t1, BODY.Y
            lde f32t, t4, BODY.R
            lod f32t, t5, PLAYER.x
            lod f32t, t6, PLAYER.y

            # Calculate Distance
            fsub t7, t5, t0 # dx
            fsub t8, t6, t1 # dy
            fpow t9, t7, 2.0
            fpow t10, t8, 2.0
            fadd t9, t10 # r^2
            fsqrt t9 # r
            fdiv t7, t9 # Normal vector
            fdiv t8, t9

            lod f32t, t11, distance_scale
            fdiv t10, t4, t11
            ffma t11, t7, t10, t0 # Move player to radius
            ffma t12, t8, t10, t1
            str f32t, PLAYER.velx, t11
            str f32t, PLAYER.vely, t12

            str u8t, PLAYER.parent_body_index, s0

            .if_just_collided:
                lod u8t, cr, PLAYER.grounded # Only apply collision impulse when not grounded
                jtr @end+
                lod f32t, a0, PLAYER.x
                lod f32t, a1, PLAYER.y
                lod f32t, a2, PLAYER.velx
                lod f32t, a3, PLAYER.vely
                lod f32t, a4, PLAYER.mass
                lde f32t, a5, BODY.X
                lde f32t, a6, BODY.Y
                lde f32t, a7, BODY.VX
                lde f32t, a8, BODY.VY
                lde f32t, a9, BODY.M
                cal get_collision_vector
                mov a2, s0
                cal apply_impulse
            @end:

            cea bodies, s0, BODY.SIZE
            lde f32t, t0, BODY.VX
            lde f32t, t1, BODY.VY

            str f32t, PLAYER.velx, t0
            str f32t, PLAYER.vely, t1
            str u8t, has_player_collided, true
            str u8t, skip_player_collision_check, true
            jmp @endif+
        @else:
            str u8t, has_player_collided, false
        @endif:

        inc s0
        jmp @loop_bodies-
    @endloop_bodies:

    lod u8t, t0, has_player_collided
    str u8t, PLAYER.grounded, t0

    vpop s0..s13
    ret

#-------------------------------------------------------------------------------
sbmk "Update Smoke"
update_smoke:
    vpsh s0..s1

    mov s1, true # Only spawn 1 smoke particle per frame

    mov s0, zr
    @loop:
        cmp lt, s0, SMOKE.MAX_PARTICLE_COUNT
        jfs @endloop+
        cea smoke, s0, SMOKE.SIZE
        lde f32t, t0, SMOKE.X
        lde f32t, t1, SMOKE.Y
        lde f32t, t2, SMOKE.VX
        lde f32t, t3, SMOKE.VY
        lde f32t, t4, SMOKE.LIFESPAN

        .if_smoke_alive:
            cmp fgt, t4, 0.0
            jfs @else+
            lod f32t, t5, dt
            fmul t2, t5
            fmul t3, t5
            fsub t4, t5
            lod f32t, t6, distance_scale
            fdiv t3, t6
            fdiv t4, t6
            fadd t0, t2
            fadd t1, t3
            ste f32t, SMOKE.X, t0
            ste f32t, SMOKE.Y, t1
            ste f32t, SMOKE.LIFESPAN, t4
            jmp @endif+
        @else:
            lod u8t, cr, smoke_can_spawn
            jfs @endif+
            mov cr, s1
            jfs @endif+
            mov a0, s0
            cal spawn_smoke
            mov s1, false
        @endif:
        inc s0
        jmp @loop-
    @endloop:
    vpop s0..s1
    ret


#-------------------------------------------------------------------------------
bmk "Drawing"

#-------------------------------------------------------------------------------
sbmk "Draw Player"
draw_player:

    .if_zoomed_in:
        lod f32t, t0, distance_scale
        cmp fgt, t0, ZOOM_IN_VAL
        jtr @else+
        mov a0, PLAYER_SPRITESHEET
        lod f32t, t0, PLAYER.x
        lod f32t, t1, PLAYER.y
        fadd t0, CENTER_X_F
        fadd t1, CENTER_Y_F
        frou a1, t0
        frou a2, t1
        fcti a1
        fcti a2
        sub a1, PLAYER_SPRITE_WIDTH/2
        sub a2, PLAYER_SPRITE_WIDTH/2
        lod f32t, t2, PLAYER.rot
        fdiv a3, t2, PI/4.0
        frou a3
        fcti a3
        mod a3, 8
        mul a3, PLAYER_SPRITE_WIDTH
        lod u8t, a4, PLAYER.flip_sprite
        mul a4, PLAYER_SPRITE_WIDTH
        mov a5, PLAYER_SPRITE_WIDTH
        mov a6, PLAYER_SPRITE_WIDTH
        mov a7, 0
        #lod u8t, a7, PLAYER.flip_sprite
        syscall SYS_DRAW_TEXTURE_REGION
        jmp @end+
    @else:
        lod f32t, t0, PLAYER.x
        lod f32t, t1, PLAYER.y
        # Get normal vector to parent body
        lod u8t, t2, PLAYER.parent_body_index
        cea bodies, t2, BODY.SIZE
        lde f32t, t2, BODY.X
        lde f32t, t3, BODY.Y
        fsub t4, t2, t0
        fsub t5, t3, t1
        fpow t6, t4, 2.0
        fpow t7, t5, 2.0
        fadd t6, t7
        fsqrt t6
        fdiv t4, t6
        fdiv t5, t6
        lod f32t, t6, PLAYER.collision_radius
        lod f32t, t7, distance_scale
        fdiv t6, t7
        fmul t4, t6
        fmul t5, t6
        fadd t0, t4
        fadd t1, t5
        fadd t0, CENTER_X_F
        fadd t1, CENTER_Y_F
        frou t0
        frou t1
        fcti t0
        fcti t1
        # Draw player as a dot
        .is_offscreen:
            cmp lt, t0, 0
            jtr @end+
            cmp lt, t1, 0
            jtr @end+
            cmp lt, t0, SCREEN_WIDTH
            jfs @end+
            cmp lt, t1, SCREEN_HEIGHT
            jfs @end+
            sbpx t0, t1, 255
        #@end:
    #lod f32t, t2, PLAYER.rot
    #fdiv t2, PI/4.0
    #fcti t2
    #mul t2, .SPRITE_WIDTH
    #mov a3, t2
    #mov a4, 16
    #mov a5, .SPRITE_WIDTH
    #mov a6, .SPRITE_WIDTH
    #lod u8t, a7, PLAYER.flipped
    #syscall SYS_DRAW_TEXTURE_REGION

    @end:
    ret

#-------------------------------------------------------------------------------
sbmk "Draw Bodies"
draw_bodies:
    # s0: body index
    psh s0

    mov s0, zr
    @loop:
        lod u8t, t0, bodies_count
        cmp lt, s0, t0
        jfs @endloop+

        cea bodies, s0, BODY.SIZE
        lde f32t, t0, BODY.X
        lde f32t, t1, BODY.Y
        fadd a0, t0, CENTER_X_F
        fadd a1, t1, CENTER_Y_F
        lde f32t, a2, BODY.R
        lod f32t, t1, distance_scale
        fdiv a2, t1
        mov a3, BODY_LUMA/2
        lod u8t, cr, zoom_toggled
        mvc a3, BODY_LUMA
        cal draw_circle

        .if_show_vectors:
            mov cr, DRAW_VELOCITIES
            jfs @endif+
            cea bodies, s0, BODY.SIZE
            lde f32t, t0, BODY.X
            lde f32t, t1, BODY.Y
            fadd a0, t0, CENTER_X_F
            fadd a1, t1, CENTER_Y_F
            lde f32t, t2, BODY.VX
            lde f32t, t3, BODY.VY
            ffma a2, t2, VELOCITY_VISUAL_SCALE, a0
            ffma a3, t3, VELOCITY_VISUAL_SCALE, a1
            cal draw_line
        @endif:

        inc s0
        jmp @loop-
    @endloop:
    pop s0
    ret

#-------------------------------------------------------------------------------
sbmk "Draw Smoke"
draw_smoke:
    mov t15, zr
    @loop:
        cmp lt, t15, SMOKE.MAX_PARTICLE_COUNT
        jfs @endloop+
        cea smoke, t15, SMOKE.SIZE
        lde f32t, t0, SMOKE.X
        lde f32t, t1, SMOKE.Y
        lde f32t, t2, SMOKE.LIFESPAN
        .if_smoke_alive:
            cmp fgt, t2, 0.0
            jfs @endif+
            fadd t0, CENTER_X_F
            fadd t1, CENTER_Y_F
            frou t0
            frou t1
            fcti t0
            fcti t1
            .if_in_bounds:
                cmp gt, t0, 0
                jfs @endif2+
                cmp gt, t1, 0
                jfs @endif2+
                cmp lt, t0, SCREEN_WIDTH
                jfs @endif2+
                cmp lt, t1, SCREEN_HEIGHT
                jfs @endif2+
                mov t3, 100 # Use a dimmer color when zoomed out
                lod u8t, cr, zoom_toggled
                mvc t3, 255
                sbpx t0, t1, t3
            @endif2:
        @endif:
    inc t15
    jmp @loop-
    @endloop:
    ret

#-------------------------------------------------------------------------------
sbmk "Draw Circle"
draw_circle:
    # > (f32t) a0..a1: x,y center
    # > (f32t) a2: radius
    # > (u8t) a3: luma

    .is_circle_offscreen:
        fadd t0, a0, a2
        cmp flt, t0, 0.0
        jtr @end+
        fadd t0, a1, a2
        cmp flt, t0, 0.0
        jtr @end+
        fsub t0, a0, a2
        cmp fgt, t0, SCREEN_WIDTH_F
        jtr @end+
        fsub t0, a1, a2
        cmp fgt, t0, SCREEN_HEIGHT_F
        jtr @end+

    fdiv t14, 1.0, a2 # How much to increment angle by
    mov t15, zr # t15: angle
    @loop:
        fcos t0, t15 # t0: r*cos(t) = x
        ffma t0, a2, a0
        fsin t1, t15 # t1: r*sin(t) = y
        ffma t1, a2, a1
        fcti t0
        fcti t1
        .if_in_bounds:
            cmp gt, t0, 0
            jfs @endif+
            cmp gt, t1, 0
            jfs @endif+
            cmp lt, t0, SCREEN_WIDTH
            jfs @endif+
            cmp lt, t1, SCREEN_HEIGHT
            jfs @endif+
            sbpx t0, t1, a3
        @endif:
        fadd t15, t14 # increment angle
        cmp fgt, t15, 2.0*PI
        jtr @end+
        jmp @loop-
    @end:
    ret

#-------------------------------------------------------------------------------
sbmk "Draw Line"
draw_line:
    # > a0..a1: start x,y
    # > a2..a3: end x,y

    vpsh s0..s3
    mov s0, a0
    mov s1, a1
    mov s2, a2
    mov s3, a3
    fsub t0, s2, s0
    fsub t1, s3, s1
    fpow t4, t0, 2.0
    fpow t5, t1, 2.0
    fadd t4, t5
    fsqrt t4 # Length
    fatan2 t7, t1, t0
    fcos t2, t7 # dx
    fsin t3, t7 # dy
    mov t0, s0
    mov t1, s1
    mov t15, zr
    @loop:
        frou t5, t0
        frou t6, t1
        fcti t5
        fcti t6
        .if_in_bounds:
            cmp gt, t5, 0
            jfs @endif+
            cmp gt, t6, 0
            jfs @endif+
            cmp lt, t5, SCREEN_WIDTH
            jfs @endif+
            cmp lt, t6, SCREEN_HEIGHT
            jfs @endif+
            sbpx t5, t6, BODY_LUMA
        @endif:
        fadd t0, t2
        fadd t1, t3
        fadd t15, 1.0
        cmp flt, t15, t4
        jtr @loop-
    @end:
    vpop s0..s3
    ret

#-------------------------------------------------------------------------------
bmk "Physics"

#-------------------------------------------------------------------------------
sbmk "Check Collision"
check_collision:
    # > a0: body1 index
    # > a1: body2 index
    cea bodies, a0, BODY.SIZE
    lde f32t, t0, BODY.X
    lde f32t, t1, BODY.Y
    lde f32t, t2, BODY.VX
    lde f32t, t3, BODY.VY
    lde f32t, t4, BODY.R

    cea bodies, a1, BODY.SIZE
    lde f32t, t5, BODY.X
    lde f32t, t6, BODY.Y
    lde f32t, t7, BODY.VX
    lde f32t, t8, BODY.VY
    lde f32t, t9, BODY.R

    # Apply deltatime
    lod f32t, t10, dt
    fmul t2, t10
    fmul t3, t10
    fmul t7, t10
    fmul t8, t10

    # Apply distance scale
    lod f32t, t10, distance_scale
    fdiv t2, t10
    fdiv t3, t10
    fdiv t7, t10
    fdiv t8, t10

    # Add velocities
    fadd t0, t2
    fadd t1, t3
    fadd t5, t7
    fadd t6, t8

    # Calculate Distance
    fsub t2, t5, t0 # dx
    fsub t3, t6, t1 # dy
    fpow t2, 2.0
    fpow t3, 2.0
    fadd t2, t3 # r^2
    fsqrt t2 # r

    # Add Radii
    fadd t4, t9
    lod f32t, t10, distance_scale
    fdiv t4, t10

    # Check if distance is less than radii
    cmp flt, t2, t4
    ret

#-------------------------------------------------------------------------------
sbmk "Check Player Collision"
check_player_collision:
    # > a0: body index
    cea bodies, a0, BODY.SIZE
    lde f32t, t0, BODY.X
    lde f32t, t1, BODY.Y
    lde f32t, t2, BODY.VX
    lde f32t, t3, BODY.VY
    lde f32t, t4, BODY.R

    lod f32t, t5, PLAYER.x
    lod f32t, t6, PLAYER.y
    lod f32t, t7, PLAYER.velx
    lod f32t, t8, PLAYER.vely

    # Apply deltatime
    lod f32t, t10, dt
    fmul t2, t10
    fmul t3, t10
    fmul t7, t10
    fmul t8, t10

    # Apply distance scale
    lod f32t, t10, distance_scale
    fdiv t2, t10
    fdiv t3, t10
    fdiv t7, t10
    fdiv t8, t10

    # Add velocities
    fadd t0, t2
    fadd t1, t3
    fadd t5, t7
    fadd t6, t8

    # Calculate Distance
    fsub t2, t0, t5 # dx
    fsub t3, t1, t6 # dy
    fpow t2, 2.0
    fpow t3, 2.0
    fadd t2, t3 # r^2
    fsqrt t2 # r

    # Apply distance scale
    lod f32t, t6, PLAYER.collision_radius
    fadd t4, t6
    lod f32t, t5, distance_scale
    fdiv t4, t5

    # Check if distance is less than body radius
    cmp flt, t2, t4
    ret

#-------------------------------------------------------------------------------
sbmk "Get Gravity Vector"
get_gravity_vector:
    # > a0..a1: object1 pos
    # > a2..a3: object1 velocity
    # > a4: object1 mass
    # > a5..a6: object2 pos
    # > a7: object2 mass
    # < a0..a1: Acceleration vector
    # < a2: Force

    # Calculate Distance
    fsub t0, a5, a0 # dx
    fsub t1, a6, a1 # dy
    fpow t2, t0, 2.0
    fpow t3, t1, 2.0
    fadd t2, t3 # r^2

    # Normal Vector
    fsqrt t3, t2 # r
    fdiv t0, t3 # dx/r
    fdiv t1, t3 # dy/r

    # Scale Distance
    lod f32t, t5, distance_scale
    fmul t3, t5
    fpow t2, t3, 2.0 # r^2

    # Calculate gravitational acceleration
    fmul t3, a7, G # G*m2 since we'll be dividing by m1 anyway, no point including it
    fdiv t3, t2

    # Apply dt
    lod f32t, t4, dt
    fmul t3, t4

    # Apply timescale
    @if_timescale_lt_0:
        cmp flt, TIME_SCALE, 0.0
        jfs @endif+
        fmul t3, TIME_SCALE
    @endif:

    # Get acceleration vector
    fmul a0, t0, t3
    fmul a1, t1, t3
    mov a2, t3

    ret

#-------------------------------------------------------------------------------
sbmk "Get Collision Vector"
get_collision_vector:
    # > a0..a1: object1 pos
    # > a2..a3: object1 velocity
    # > a4: object1 mass
    # > a5..a6: object2 pos
    # > a7..a8: object2 velocity
    # > a9: object2 mass
    # < a0..a1: impulse vector

    # Get collision vector
    fsub t0, a5, a0 # dx
    fsub t1, a6, a1 # dy
    fpow t2, t0, 2.0
    fpow t3, t1, 2.0
    fadd t2, t3
    fsqrt t2 # r
    fdiv t0, t2 # normal vector
    fdiv t1, t2

    # Get relative velocity vector
    fsub t2, a7, a2 # relvx
    fsub t3, a8, a3 # relvy

    # Calculate vn dot product
    fmul t2, t0
    fmul t3, t1
    fadd t2, t3 # dot product

    # Calculate impulse magnitude
    fmul t3, -(1.0 + BODY_ELASTICITY), t2
    fdiv t5, 1.0, a4 # 1/m1
    fdiv t6, 1.0, a9 # 1/m2
    fadd t5, t6
    fdiv t3, t5 # impulse magnitude

    # Calculate impulse vector
    fmul a0, t0, t3
    fmul a1, t1, t3
    ret



#-------------------------------------------------------------------------------
sbmk "Apply Impulse"
apply_impulse:
    # > a0..a1: impulse vector
    # > a2: body index

    cea bodies, a2, BODY.SIZE
    lde f32t, t0, BODY.VX
    lde f32t, t1, BODY.VY
    lde f32t, t2, BODY.M

    fdiv t3, a0, t2
    fdiv t4, a1, t2
    fadd t0, t3
    fadd t1, t4

    ste f32t, BODY.VX, t0
    ste f32t, BODY.VY, t1

    ret

#-------------------------------------------------------------------------------
bmk "Camera"

#-------------------------------------------------------------------------------
sbmk "Move Camera"
move_camera:
    # > a0..a1: Camera offset

    mov t15, zr # Object counter
    lod u8t, t14, bodies_count
    @loop:
        cmp lt, t15, t14
        jfs @endloop+

        cea bodies, t15, BODY.SIZE
        lde f32t, t0, BODY.X
        lde f32t, t1, BODY.Y
        fadd t0, a0
        fadd t1, a1
        ste f32t, BODY.X, t0
        ste f32t, BODY.Y, t1

        inc t15
        jmp @loop-
    @endloop:

    mov t15, zr
    @loop:
        cmp lt, t15, SMOKE.MAX_PARTICLE_COUNT
        jfs @endloop+
        cea smoke, t15, SMOKE.SIZE
        lde f32t, t0, SMOKE.X
        lde f32t, t1, SMOKE.Y
        fadd t0, a0
        fadd t1, a1
        ste f32t, SMOKE.X, t0
        ste f32t, SMOKE.Y, t1

        inc t15
        jmp @loop-
    @endloop:

    lod f32t, t0,  PLAYER.x
    lod f32t, t1,  PLAYER.y
    fadd t0, a0
    fadd t1, a1
    str f32t, PLAYER.x, t0
    str f32t, PLAYER.y, t1


    ret

#-------------------------------------------------------------------------------
sbmk "Center Camera"
center_camera:
    lod f32t, a0, PLAYER.x
    lod f32t, a1, PLAYER.y
    fneg t0, a0
    fneg t1, a1
    lod f32t, t2, distance_scale
    fdiv t2, CAMERA_SPEED, t2

    .if_grounded:
        lod u8t, cr, PLAYER.grounded
        jfs @endif+
        lod f32t, t3, PLAYER.rot
        fcos t4, t3
        fsin t5, t3
        ffma t0, t4, -CAMERA_OFFSET, t0
        ffma t1, t5, -CAMERA_OFFSET, t1
    @endif:
    flrp a0, 0.0, t0, t2
    flrp a1, 0.0, t1, t2

    cal move_camera
    #str f32t, PLAYER.x, 0.0
    #str f32t, PLAYER.y, 0.0
    ret

#-------------------------------------------------------------------------------
sbmk "Set Distance Scale"
set_distance_scale:
    # Scales distance from center of camera
    # > a0: Distance Scale

    lod f32t, t13, distance_scale
    mov t15, zr # Object counter
    lod u8t, t14, bodies_count
    @loop:
        cmp lt, t15, t14
        jfs @endloop+

        cea bodies, t15, BODY.SIZE
        lde f32t, t0, BODY.X
        lde f32t, t1, BODY.Y
        # Multiply by previous distance scale
        fmul t0, t13
        fmul t1, t13
        # Divide by new distance scale
        fdiv t0, a0
        fdiv t1, a0
        # Store new position
        ste f32t, BODY.X, t0
        ste f32t, BODY.Y, t1

        inc t15
        jmp @loop-
    @endloop:
    # Player
    lod f32t, t0, PLAYER.x
    lod f32t, t1, PLAYER.y
    # Divide by previous distance scale
    fmul t0, t13
    fmul t1, t13
    # Multiply by new distance scale
    fdiv t0, a0
    fdiv t1, a0
    # Store new position
    str f32t, PLAYER.x, t0
    str f32t, PLAYER.y, t1

    str f32t, distance_scale, a0
    ret

#-------------------------------------------------------------------------------
bmk "Terminal Input"

#-------------------------------------------------------------------------------
sbmk "Check Terminal Commands"
check_commands:
    # s0: command address
    psh s0
    mov s0, buffer
    @loop:
        .zoom_in:
            mov a0, s0
            mov a1, Commands.ZOOM_IN
            cal cmp_strings
            jfs @endif+
            lod f32t, t0, distance_scale
            fsub t0, ZOOM_STEP
            fmax t0, 1.0
            mov a0, t0
            cal set_distance_scale
            mov a1, 1
            mov a0, Strings.zoom
            syscall SYS_PRINT_STRING
            lod f32t, a0, distance_scale
            syscall SYS_PRINT_LINE_FLOAT
            jmp @endloop+
        @endif:
        .zoom_out:
            mov a0, s0
            mov a1, Commands.ZOOM_OUT
            cal cmp_strings
            jfs @endif+
            lod f32t, t0, distance_scale
            fadd t0, ZOOM_STEP
            fmax t0, 1.0
            mov a0, t0
            cal set_distance_scale
            mov a0, Strings.zoom
            mov a1, 1
            syscall SYS_PRINT_STRING
            lod f32t, a0, distance_scale
            syscall SYS_PRINT_LINE_FLOAT
            jmp @endloop+
        @endif:
    @endloop:
    pop s0
    ret

#-------------------------------------------------------------------------------
bmk "Helper Functions"

#-------------------------------------------------------------------------------
sbmk "Compare Strings"
cmp_strings:
    # > a0*: string1 address
    # > a1*: string2 address
    # < cr: Are strings equal

    cmp eq, a0, a1
    jtr @endloop+

    mov t0, a0
    mov t1, a1
    @loop:
        lod u8t, t2, t0
        lod u8t, t3, t1
        .if_eq:
            cmp eq, t2, t3
            jfs @endloop+
            .if_null:
                cmp eq, t2, 0
                jtr @endloop+ # Strings terminated
                cmp eq, t2, 32 # Count spaces as terminators
                jtr @endloop+
                inc t0
                inc t1
                jmp @loop-
    @endloop:
    ret
